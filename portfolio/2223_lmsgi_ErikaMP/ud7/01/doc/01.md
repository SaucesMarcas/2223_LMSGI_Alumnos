# Unidad 7 . Almacenamiento de información. 
## XQuery - Bookstore Part I
1. Lista el título de los libros ordenados por título.
   
    ```xq
    for $x in doc("books")/bookstore/book
    order by $x/title
    return $x/title/text()
    ```

    [Resultado](../target/resultado01.txt)

2. Lista el título de los libros ordenados por título cuyo precio es superior a 30.
   
    ```xq
        for $x in doc("books")/bookstore/book
        where $x/price>30
        order by $x/title
        return $x/title/text()
    ```

    [Resultado](../target/resultado02.txt)

3. Crea una página web (03.html) cuyo título de la web “UD7-01.03 Listado de libros. Nombre y apellidos” y que contenga una lista con viñetas con los títulos de los libros.
   
    ```xq
        <html lang="es">
            <head>
                <title>UND07.01.03</title>
            </head>
            <body>
                <h1>Listado de libros</h1>
                <ul>
                    {
                        for $x in doc("books")/bookstore/book
                        where $x/price>30
                        order by $x/title
                        return <li>{$x/title/text()}</li>
                    }
                </ul>
            </body>
        </html>
    ```

    [Resultado](../target/resultado03.html)

4. Crea un documento XML(04.html), donde el elemento raíz es libros. Y si la categoría del libro es children el título aparece entre las etiquetas &lt;children&gt; y en caso contrario aparece con la etiqueta &lt;adult&gt;.
   
    ```xq
        <libros>
            {
                for $x in doc("books")/bookstore/book
                return if ($x/@category="children") 
                then <children>{$x/title/text()}</children>
                else <adult>{$x/title/text()}</adult> 
            }
        </libros>
    ```

    [Resultado](../target/resultado04.xml)

5. Crea una página web (05.html), que contenga todos los libros con su categoría, en la lista aparece distintos colores el título en función de la categoría.
    
    ```xq
        <html lang="es">
            <head>
                <meta encoding="UTF-8"/>
                <link href="style.css" rel="stylesheet" type="text/css"/>
            </head>
            <body>
                <h1>Bookstore</h1>
                <table>
                <tr>
                    <th>Titulo</th>
                    <th>Categoria</th>
                </tr>
                    {
                        for $x in doc("books")/bookstore/book
                        return 
                            <tr>
                                <td>{$x/title/text()}</td>
                                <td>{data($x/@category)}</td>
                            </tr>
                    }
                </table>
                <ul>
                    {
                        for $x in doc("books")/bookstore/book
                        order by $x/title
                        return 
                            <li class="{data($x/@category)}">
                                {$x/title/text()}
                            </li>
                    }
                </ul>
            </body>
        </html>
    ```

    [Resultado](../target/resultado05.html)

6. Lista de libros ordenados por categoría de forma descendiente, y como segundo criterio por título de forma descendiente. Mostrando el título con suprecio precio, donde el precio es mayor que 30 $. El resultado (06.txt) es un fichero de texto.
    
    ```xq
        for $x in doc("books")/bookstore/book
        where $x/price>30
        order by $x/@category descending, $x/title descending
        return concat($x/title/text(),": ",$x/price/text())
    ```

    [Resultado](../target/resultado06.txt)

7. Uso de at, for variable1 at variable2 expresiónXpath, siendo la segunda variable la posición de cada nodo. Genera el siguiente archivo XML (07.xml).
    
    ```xq
        <books>
            {
                for $x at $h in doc("books")/bookstore/book
                return 
                    <book>
                        {concat($h,". ",$x/title/text())}
                    </book>
            }
        </books>
    ```

    [Resultado](../target/resultado07.xml)

8. Uso de variables expresiones en un for $tema in (lista de valores), $y in (lista de valores). Para indicar la lista de valor de valorinicial to valorfinal, por ejemplo 1 to 100. Generar automáticamente la siguiente página web (08.html).
    
    ```xq
        <html lang="ES">
            <head>
                <meta enconding="UTF-8"/>
                <title>UND07.01.03</title>
            </head>
            <body>
                <h1>Listado de Práticas</h1>
                {
                for $x in (1 to 7), $y in (1 to 3)
                return 
                    <div>
                        <a href="{$x}/{$y}.html">
                            Tema {$x}-Ejercicio {$y}
                        </a>
                    </div>
                }
            </body>
        </html>
    ```

    [Resultado](../target/resultado08.html)

9. Mostrar el título del libro cuando tenga más de dos autores.
    
    ```xq
        for $x in doc("books")/bookstore/book
        let $f := $x/author
        where count($f)>2
        return $x/title/text()
    ```

    ```xq
        for $x in doc("books")/bookstore/book
        where count($x/author)>2
        return $x/title/text()
    ```

    [Resultado](../target/resultado09.txt)

10. Trasformar los datos a un archivo XML con el siguiente contenido, donde el atributo autores indica el número de autores de cada libro.
    
    ```xq
        <libros>
            {
                for $x in doc("books")/bookstore/book
                return 
                    <libro autores="{count($x/author)}">
                        {$x/title/text()}
                    </libro>
            }
        </libros>
    ```

    [Resultado](../target/resultado10.xml)

